;;*****************************************************************************
;;
;; Title:       CodeGen.dbl
;;
;; Type:        Program
;;
;; Description: Template based code generator
;;
;; Date:        19th March 2007
;;
;; Author:      Steve Ives, Synergex Professional Services Group
;;              http://www.synergex.com
;;
;;*****************************************************************************
;;
;; Copyright (c) 2012, Synergex International, Inc.
;; All rights reserved.
;;
;; Redistribution and use in source and binary forms, with or without
;; modification, are permitted provided that the following conditions are met:
;;
;; * Redistributions of source code must retain the above copyright notice,
;;   this list of conditions and the following disclaimer.
;;
;; * Redistributions in binary form must reproduce the above copyright notice,
;;   this list of conditions and the following disclaimer in the documentation
;;   and/or other materials provided with the distribution.
;;
;; THIS SOFTWARE IS PROVIDED BY THE COPYRIGHT HOLDERS AND CONTRIBUTORS "AS IS"
;; AND ANY EXPRESS OR IMPLIED WARRANTIES, INCLUDING, BUT NOT LIMITED TO, THE
;; IMPLIED WARRANTIES OF MERCHANTABILITY AND FITNESS FOR A PARTICULAR PURPOSE
;; ARE DISCLAIMED. IN NO EVENT SHALL THE COPYRIGHT HOLDER OR CONTRIBUTORS BE
;; LIABLE FOR ANY DIRECT, INDIRECT, INCIDENTAL, SPECIAL, EXEMPLARY, OR
;; CONSEQUENTIAL DAMAGES (INCLUDING, BUT NOT LIMITED TO, PROCUREMENT OF
;; SUBSTITUTE GOODS OR SERVICES; LOSS OF USE, DATA, OR PROFITS; OR BUSINESS
;; INTERRUPTION) HOWEVER CAUSED AND ON ANY THEORY OF LIABILITY, WHETHER IN
;; CONTRACT, STRICT LIABILITY, OR TORT (INCLUDING NEGLIGENCE OR OTHERWISE)
;; ARISING IN ANY WAY OUT OF THE USE OF THIS SOFTWARE, EVEN IF ADVISED OF THE
;; POSSIBILITY OF SUCH DAMAGE.
;;
;;*****************************************************************************

import CodeGen.Engine
.ifdef DBLNET
import Microsoft.Win32
import System.IO
.endc

main CodeGen

	record
        tt              ,int														;;Terminal channel
		ErrStatus       ,int														;;Problem?
		TxtLen          ,int														;;Length of a string
		UnsetTemplate   ,boolean													;;Need to unset CODEGEN_TPLDIR
		.ifdef D_GUI
		UnsetExe        ,boolean													;;Need to unset CODEGEN_EXE
		UnsetAuthor     ,boolean													;;Need to unset CODEGEN_AUTHOR
		UnsetCompany    ,boolean													;;Need to unset CODEGEN_COMPANY
		.endc
		TempString      ,a256
	endrecord

proc

    open(Tt=0,i,"tt:")
    flags(7004020,1)

;;------------------------------------------------------------------------------
.ifdef DBLNET
;;------------------------------------------------------------------------------
	
	;;Explicitly set CODEGEN_EXE to the location of the running executable
	TempString = Path.GetDirectoryName(System.Reflection.Assembly.GetEntryAssembly().Location)
	xcall setlog("CODEGEN_EXE",atrim(TempString),TxtLen)
	UnsetExe=true
	
	;;Is CODEGEN_TPLDIR already set in the environment?
	xcall getlog("CODEGEN_TPLDIR",TempString,TxtLen)
	if (!TxtLen)
	begin
		;;No, look in the registry for the value set by InstallShield
		data templateFolder, string
		data templateFolderFound, boolean, false

		templateFolder = (string)Registry.GetValue("HKEY_LOCAL_MACHINE\SOFTWARE\Synergex\CodeGen","TemplatePath","")
        if ((templateFolder!=^null)&&(templateFolder!="")) then
		begin
			;;Found it
			xcall setlog("CODEGEN_TPLDIR",templateFolder,TxtLen)
			templateFolderFound = true
		end
		else
		begin
			;;Not found. If we're a 64-bit process then try looking in the 32-bit registry
			if (Environment.Is64BitProcess)
			begin
				templateFolder = (string)Registry.GetValue("HKEY_LOCAL_MACHINE\SOFTWARE\Wow6432Node\Synergex\CodeGen","TemplatePath","")
				if ((templateFolder!=^null)&&(templateFolder!=""))
				begin
					;;Found it
					xcall setlog("CODEGEN_TPLDIR",templateFolder,TxtLen)
					templateFolderFound = true
				end
			end
		end

		;;Still nothing? Default to current directory
		if (!templateFolderFound)
			xcall setlog("CODEGEN_TPLDIR",".",TxtLen)

		UnsetTemplate=true
	end
	
	;;Is CODEGEN_AUTHOR already set in the environment?
	xcall getlog("CODEGEN_AUTHOR",TempString,TxtLen)
	if (!TxtLen)
	begin
		;;No, look in the registry for the value set by InstallShield
		TempString = (string)Registry.GetValue("HKEY_LOCAL_MACHINE\SOFTWARE\Synergex\CodeGen","DefaultAuthor","")
        if (TempString) then
		begin
			xcall setlog("CODEGEN_AUTHOR",TempString,TxtLen)
			UnsetAuthor = true
		end
		else
		begin
			;;Not found. If we're a 64-bit process then try looking in the 32-bit registry
			TempString = (string)Registry.GetValue("HKEY_LOCAL_MACHINE\SOFTWARE\Wow6432Node\Synergex\CodeGen","DefaultAuthor","")
			if (TempString)
			begin
				xcall setlog("CODEGEN_AUTHOR",TempString,TxtLen)
				UnsetAuthor = true
			end
		end
	end
	
	;;Is CODEGEN_COMPANY already set in the environment?
	xcall getlog("CODEGEN_COMPANY",TempString,TxtLen)
	if (!TxtLen)
	begin
		;;No, look in the registry for the value set by InstallShield
		TempString = (string)Registry.GetValue("HKEY_LOCAL_MACHINE\SOFTWARE\Synergex\CodeGen","DefaultCompany","")
        if (TempString) then
		begin
			xcall setlog("CODEGEN_COMPANY",TempString,TxtLen)
			UnsetCompany = true
		end
		else
		begin
			;;Not found. If we're a 64-bit process then try looking in the 32-bit registry
			TempString = (string)Registry.GetValue("HKEY_LOCAL_MACHINE\SOFTWARE\Wow6432Node\Synergex\CodeGen","DefaultCompany","")
			if (TempString)
			begin
				xcall setlog("CODEGEN_COMPANY",TempString,TxtLen)
				UnsetCompany = true
			end
		end
	end
	
	xcall CodeGenLauncher
	
;;------------------------------------------------------------------------------
.else  ;End of Synergy .NET, start of Traditional Synergy code
;;------------------------------------------------------------------------------
	
	if (!ErrStatus)
	begin
		;;Is CODEGEN_EXE already set in the environment?
		xcall getlog("CODEGEN_EXE",TempString,TxtLen)
		if (!TxtLen)
		begin
			writes(Tt,"ERROR: CodeGen is not correctly installed (can't determine CODEGEN_EXE)")
			ErrStatus=1
		end
	end
	
	if (!ErrStatus)
	begin
		;;Is CODEGEN_TPLDIR already set in the environment?
		xcall getlog("CODEGEN_TPLDIR",TempString,TxtLen)
		if (!TxtLen)
		begin
			;;No, default to current directory
			.ifdef OS_VMS
			xcall setlog("CODEGEN_TPLDIR","SYS$DISK:",TxtLen)
			.else
			xcall setlog("CODEGEN_TPLDIR",".",TxtLen)
			.endc
			UnsetTemplate=true
		end
	end
	
	;;Off we go
	if (!ErrStatus)
	begin
		close Tt
		clear Tt
		xcall CodeGenLauncher
	end
	
;;------------------------------------------------------------------------------
.endc ;End of traditional Synergy processing
;;------------------------------------------------------------------------------
	
	;;Unset any environment variables that we set
	if (UnsetTemplate)
		xcall setlog("CODEGEN_TPLDIR",,TxtLen)
	.ifdef D_GUI
	if (UnsetExe)
		xcall setlog("CODEGEN_EXE",,TxtLen)
	if (UnsetAuthor)
		xcall setlog("CODEGEN_AUTHOR",,TxtLen)
	if (UnsetCompany)
		xcall setlog("CODEGEN_COMPANY",,TxtLen)
	.endc
	
    stop

endmain
;;*****************************************************************************
;;
;; Title:       SelectionTokenTests.dbl
;;
;; Type:        Unit test class
;;
;; Description: Unit tests for CodeGenEngine.ExpandFieldSelectionToken()
;;
;; Date:        28th November 2012
;;
;; Author:      Steve Ives, Synergex Professional Services Group
;;              http://www.synergex.com
;;
;;*****************************************************************************
;;
;; Copyright (c) 2012, Synergex International, Inc.
;; All rights reserved.
;;
;; Redistribution and use in source and binary forms, with or without
;; modification, are permitted provided that the following conditions are met:
;;
;; * Redistributions of source code must retain the above copyright notice,
;;   this list of conditions and the following disclaimer.
;;
;; * Redistributions in binary form must reproduce the above copyright notice,
;;   this list of conditions and the following disclaimer in the documentation
;;   and/or other materials provided with the distribution.
;;
;; THIS SOFTWARE IS PROVIDED BY THE COPYRIGHT HOLDERS AND CONTRIBUTORS "AS IS"
;; AND ANY EXPRESS OR IMPLIED WARRANTIES, INCLUDING, BUT NOT LIMITED TO, THE
;; IMPLIED WARRANTIES OF MERCHANTABILITY AND FITNESS FOR A PARTICULAR PURPOSE
;; ARE DISCLAIMED. IN NO EVENT SHALL THE COPYRIGHT HOLDER OR CONTRIBUTORS BE
;; LIABLE FOR ANY DIRECT, INDIRECT, INCIDENTAL, SPECIAL, EXEMPLARY, OR
;; CONSEQUENTIAL DAMAGES (INCLUDING, BUT NOT LIMITED TO, PROCUREMENT OF
;; SUBSTITUTE GOODS OR SERVICES; LOSS OF USE, DATA, OR PROFITS; OR BUSINESS
;; INTERRUPTION) HOWEVER CAUSED AND ON ANY THEORY OF LIABILITY, WHETHER IN
;; CONTRACT, STRICT LIABILITY, OR TORT (INCLUDING NEGLIGENCE OR OTHERWISE)
;; ARISING IN ANY WAY OUT OF THE USE OF THIS SOFTWARE, EVEN IF ADVISED OF THE
;; POSSIBILITY OF SUCH DAMAGE.
;;
;;*****************************************************************************

import Microsoft.VisualStudio.TestTools.UnitTesting
import System

namespace CodeGen.UnitTests
	
	{TestClass}
	public class SelectionTokenTests
		
		private mStr			,@CodeGen.RepositoryAPI.RpsStructure
		private mFieldTwo		,@CodeGen.RepositoryAPI.RpsField
		private mMsg			,String
		
		{TestInitialize}
		public method Initialize, void
			endparams
		proc
			;;Make sure we don't try to display anything to the screen
			CodeGen.Engine.Env.SuppressLogging = true
			
			;;Load the UNIT_TEST repository structure
			mStr = new CodeGen.RepositoryAPI.RpsStructure("UNIT_TEST")
			
			;;Run the structure through CheckStructure() because it sets up
			;;some stuff that is needed in order to successfully process some tokens
			if (!CodeGen.Engine.CheckStructure(mStr))
				throw new Exception("CodeGen.Engine.CheckStructure failed to validate structure UNIT_TEST")
				
			;;Get handles on the fields we'll use during our tests
			mFieldTwo   = mStr.Fields[1]	;D1 enumerated with selections
			
			;;Configure CodeGen for repository processing
			CodeGen.Engine.Env.RpsInUse = true
			
		endmethod
		
		{TestMethod}
		{TestCategory("Field Selection Loop Token Tests")}
		public method SelectionToken_SelectionNumber, void
			endparams
		proc
			Assert.IsTrue(doTest(mFieldTwo,2,"<SELECTION_NUMBER>","2"),mMsg)
		endmethod
		
		{TestMethod}
		{TestCategory("Field Selection Loop Token Tests")}
		public method SelectionToken_SelectionCount, void
			endparams
		proc
			Assert.IsTrue(doTest(mFieldTwo,1,"<SELECTION_COUNT>","3"),mMsg)
		endmethod
		
		{TestMethod}
		{TestCategory("Field Selection Loop Token Tests")}
		public method SelectionToken_SelectionText, void
			endparams
		proc
			Assert.IsTrue(doTest(mFieldTwo,1,"<SELECTION_TEXT>","Option1"),mMsg)
		endmethod
		
		{TestMethod}
		{TestCategory("Field Selection Loop Token Tests")}
		public method SelectionToken_SelectionValue, void
			endparams
		proc
			Assert.IsTrue(doTest(mFieldTwo,3,"<SELECTION_VALUE>","3"),mMsg)
		endmethod
		
.region "Support methods"
		
		private method doTest, Boolean
			required in fld			,@CodeGen.RepositoryAPI.RpsField
			required in selNbr		,int	;1-based here, but 0-based in ExpandFieldSelectionToken()
			required in inBuffer	,String
			required in expBuffer	,String
			endparams
		proc
			data ok				,Boolean
			data mainBuffer		,a32768	,inBuffer
			data expectedResult	,a32768	,expBuffer
			data tokenStart		,int	,instr(1,mainBuffer,"<") 
			data tokenEnd		,int	,instr(tokenStart,mainBuffer,">")
			
			mMsg = "PROCESSING: " + atrim(mainBuffer) + " EXPECTED: " + atrim(expectedResult)
			
			if (ok=CodeGen.Engine.ExpandFieldSelectionToken(fld,selNbr-1,tokenStart,tokenEnd,false,mainBuffer))
				ok = (mainBuffer.eqs.expectedResult)
				
			if (!ok)
				mMsg = mMsg + " ACTUAL: " + atrim(mainBuffer)
				
			mreturn ok
			
		endmethod
		
.endregion
		
	endclass
	
endnamespace


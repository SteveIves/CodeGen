;;*****************************************************************************
;;
;; Title:       CreateFile.dbl
;;
;; Type:        Program
;;
;; Description: Create data files from repository definitions.
;;
;; Date:        3rd April 2007
;;
;; Author:      Steve Ives, Synergex Professional Services Group
;;              http://www.synergex.com
;;
;;*****************************************************************************
;;
;; Copyright (c) 2012, Synergex International, Inc.
;; All rights reserved.
;;
;; Redistribution and use in source and binary forms, with or without
;; modification, are permitted provided that the following conditions are met:
;;
;; * Redistributions of source code must retain the above copyright notice,
;;   this list of conditions and the following disclaimer.
;;
;; * Redistributions in binary form must reproduce the above copyright notice,
;;   this list of conditions and the following disclaimer in the documentation
;;   and/or other materials provided with the distribution.
;;
;; THIS SOFTWARE IS PROVIDED BY THE COPYRIGHT HOLDERS AND CONTRIBUTORS "AS IS"
;; AND ANY EXPRESS OR IMPLIED WARRANTIES, INCLUDING, BUT NOT LIMITED TO, THE
;; IMPLIED WARRANTIES OF MERCHANTABILITY AND FITNESS FOR A PARTICULAR PURPOSE
;; ARE DISCLAIMED. IN NO EVENT SHALL THE COPYRIGHT HOLDER OR CONTRIBUTORS BE
;; LIABLE FOR ANY DIRECT, INDIRECT, INCIDENTAL, SPECIAL, EXEMPLARY, OR
;; CONSEQUENTIAL DAMAGES (INCLUDING, BUT NOT LIMITED TO, PROCUREMENT OF
;; SUBSTITUTE GOODS OR SERVICES; LOSS OF USE, DATA, OR PROFITS; OR BUSINESS
;; INTERRUPTION) HOWEVER CAUSED AND ON ANY THEORY OF LIABILITY, WHETHER IN
;; CONTRACT, STRICT LIABILITY, OR TORT (INCLUDING NEGLIGENCE OR OTHERWISE)
;; ARISING IN ANY WAY OUT OF THE USE OF THIS SOFTWARE, EVEN IF ADVISED OF THE
;; POSSIBILITY OF SUCH DAMAGE.
;;
;;*****************************************************************************

import CodeGen.Engine

namespace CreateFile
	
	;;; <summary>
	;;; The main entry point for the application.
	;;; </summary>
	main CreateFile
	
		.define APP_NAME        "CreateFile"
		.define APP_VERSION     "V2.0"
	
		.include "RPSLIB:ddinfo.def"
	
		record
			tt					,i4			;;Terminal channel
			ok                  ,i4			;;OK to continue
			filename            ,[1]a30     ;Structure to process
			strname             ,[1]a30     ;Structure to process
			filespec            ,[1]a255    ;File to create
			filecreated         ,a255       ;File created
			errtxt              ,a60        ;Error text
			replace             ,d1         ;Replace existing files?
			rpsopen             ,d1         ;Is the repository open?
		endrecord
		
		structure struct
			,a30
		endstructure
		
	proc
		
		ok=TRUE

		open(tt=0,i,"tt:")
		xcall flags(7004000,1)
		xcall flags(20,0)
		
		;---------------------------------------------------------------------------
		;Are we being asked for usage information?
		
		if (CommandLine("h "))
		begin
			call usage
			ok = false
		end
		
		;---------------------------------------------------------------------------
		;Do we have a file name?
		
		if (ok)
		begin
			if (CommandLine("f ",1,filename))
			begin
				if (!filename[1])
				begin
					writes(tt,"No file name specified following -f. Use -h for help.")
					ok=FALSE
				end
			end
		end
		
		;---------------------------------------------------------------------------
		;Do we have a structure name?
		
		if (ok)
		begin
			if (CommandLine("s ",1,strname))
			begin
				if (!strname[1])
				begin
					writes(tt,"No structure name specified following -s. Use -h for help.")
					ok=FALSE
				end
			end
		end

		;---------------------------------------------------------------------------
		;Make sure we have a file name or a structure name
		
		if (ok)
		begin
			if (!filename[1]&&!strname[1])
			begin
				writes(tt,"You must specify a file (-f) or structure (-s). Use -h for help.")
				ok=FALSE
			end
		end
		
		;---------------------------------------------------------------------------
		;Make sure we don't have both file and structure names
		
		if (ok)
		begin
			if (filename[1]&&strname[1])
			begin
				writes(tt,"Options -f and -s can't be used together. Use -h for help.")
				ok=FALSE
			end
		end
		
		;---------------------------------------------------------------------------
		;Check for a filespec option
		
		if (ok)
		begin
			if (CommandLine("out",1,filespec))
			begin
				;;Nothing to do, we'll just pass it along
				nop
			end
		end
		
		;---------------------------------------------------------------------------
		;Check for "replace existing files" option
		
		if (ok)
		begin
			if (CommandLine("r"))
				replace=TRUE
		end
		
		;---------------------------------------------------------------------------
		;Open the repository
		
		if (ok)
		begin
			xcall dd_init(dcs)
			if (dcs.error) then
			begin
				writes(tt,"Failed to open repository!")
				ok=FALSE
			end
			else
				rpsopen=TRUE
		end
		
		;---------------------------------------------------------------------------
		;Create the file
		
		if (ok)
		begin
			data success, boolean

			if (filename[1]) then
				success = CreateFileFromRpsFile(dcs,filename[1],replace,filespec[1],filecreated,errtxt)
			else
				success = CreateFileFromRpsStruct(dcs,strname[1],replace,filespec[1],filecreated,errtxt)

			if (success) then
				writes(tt,%atrim(filecreated) + " -> " + %atrim(errtxt))
			else
				writes(tt,%atrim(errtxt))
		end
		
		;---------------------------------------------------------------------------
		;Clean up and exit
		
		if (rpsopen)
			xcall dd_exit(dcs)
			
		close tt
		sleep 2
		stop
		
	usage,
		
		writes(tt,"")
		writes(tt,APP_NAME + " " + APP_VERSION)
		
		;Limit for text    -------------------------------------------------------------------------------
		writes(tt,"")
		writes(tt,"  CreateFile -f <fname> | -s <sname> [-out <filespec>] [-r] [-h]")
		writes(tt,"")
		writes(tt,"    -f <fname>")
		writes(tt,"          Name of repository file definition to process.")
		writes(tt,"")
		writes(tt,"    -s <sname>")
		writes(tt,"          Name of repository structure definition to process.")
		writes(tt,"")
		writes(tt,"    -out <filespec>")
		writes(tt,"          File spec of file to create, overriding repository file spec.")
		writes(tt,"")
		writes(tt,"    -r    Replace existing files. The default is not to replace existing files.")
		writes(tt,"")
		writes(tt,"    -h    Display this usage information.")
		writes(tt,"")
		
		return
		
	endmain

endnamespace


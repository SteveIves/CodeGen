;;*****************************************************************************
;;
;; Title:       CreateFileFromRpsStruct.dbl
;;
;; Type:        Function
;;
;; Description: Creates a file from a repository structure definition
;;
;; Date:        12th July 2000
;;
;; Author:      Paul H Fenton, Synergex Professional Services Group
;;              http://www.synergex.com
;;
;;*****************************************************************************
;;
;; Copyright (c) 2012, Synergex International, Inc.
;; All rights reserved.
;;
;; Redistribution and use in source and binary forms, with or without
;; modification, are permitted provided that the following conditions are met:
;;
;; * Redistributions of source code must retain the above copyright notice,
;;   this list of conditions and the following disclaimer.
;;
;; * Redistributions in binary form must reproduce the above copyright notice,
;;   this list of conditions and the following disclaimer in the documentation
;;   and/or other materials provided with the distribution.
;;
;; THIS SOFTWARE IS PROVIDED BY THE COPYRIGHT HOLDERS AND CONTRIBUTORS "AS IS"
;; AND ANY EXPRESS OR IMPLIED WARRANTIES, INCLUDING, BUT NOT LIMITED TO, THE
;; IMPLIED WARRANTIES OF MERCHANTABILITY AND FITNESS FOR A PARTICULAR PURPOSE
;; ARE DISCLAIMED. IN NO EVENT SHALL THE COPYRIGHT HOLDER OR CONTRIBUTORS BE
;; LIABLE FOR ANY DIRECT, INDIRECT, INCIDENTAL, SPECIAL, EXEMPLARY, OR
;; CONSEQUENTIAL DAMAGES (INCLUDING, BUT NOT LIMITED TO, PROCUREMENT OF
;; SUBSTITUTE GOODS OR SERVICES; LOSS OF USE, DATA, OR PROFITS; OR BUSINESS
;; INTERRUPTION) HOWEVER CAUSED AND ON ANY THEORY OF LIABILITY, WHETHER IN
;; CONTRACT, STRICT LIABILITY, OR TORT (INCLUDING NEGLIGENCE OR OTHERWISE)
;; ARISING IN ANY WAY OUT OF THE USE OF THIS SOFTWARE, EVEN IF ADVISED OF THE
;; POSSIBILITY OF SUCH DAMAGE.
;;
;;*****************************************************************************
;;
namespace CreateFile

	function CreateFileFromRpsStruct, boolean
		required in  a_dcs          ,a		;;Repository control data
		required in  a_structure    ,a		;;Sturture name
		optional in  a_overwrite    ,n		;;Overwrite file if found
		optional in  a_filespec     ,a		;;Filename to create
		optional out a_filecreated  ,a		;;File name created
		optional out a_errtxt       ,a		;;Error text
		endparams
		
		.include "RPSLIB:ddinfo.def"
		
		stack record
			ok						,boolean
			strname					,a30
			errtxt					,a80
		endrecord
		
	proc
		
		ok = true
		clear errtxt
		dcs = a_dcs
		strname = a_structure
		upcase strname
		
		;;Get structure information
		xcall dd_struct(dcs,DDS_INFO,strname,s_info)
		if (dcs.error) then
		begin
			ok = false
			errtxt = "Structure not found"
		end
		else
		begin
			;;Make sure the structure is assigned to at least one file
			if (!si_nmfils)
			begin
				ok = false
				errtxt = "Structure has no file assignments!"
			end
		end
		
		if (ok)
			ok = DoCreateFile(dcs,si_file,a_filespec,a_overwrite,a_filecreated,errtxt)

		if (^passed(a_errtxt))
			a_errtxt = errtxt
		
		freturn ok
		
	endfunction
	
endnamespace
;;*****************************************************************************
;;
;; Title:       RpsStructure.dbl
;;
;; Type:        Class
;;
;; Description: Represents a single Repository structure definition
;;
;; Date:        19th October 2007
;;
;; Author:      Steve Ives, Synergex Professional Services Group
;;              http://www.synergex.com
;;
;;*****************************************************************************
;;
;; Copyright (c) 2012, Synergex International, Inc.
;; All rights reserved.
;;
;; Redistribution and use in source and binary forms, with or without
;; modification, are permitted provided that the following conditions are met:
;;
;; * Redistributions of source code must retain the above copyright notice,
;;   this list of conditions and the following disclaimer.
;;
;; * Redistributions in binary form must reproduce the above copyright notice,
;;   this list of conditions and the following disclaimer in the documentation
;;   and/or other materials provided with the distribution.
;;
;; THIS SOFTWARE IS PROVIDED BY THE COPYRIGHT HOLDERS AND CONTRIBUTORS "AS IS"
;; AND ANY EXPRESS OR IMPLIED WARRANTIES, INCLUDING, BUT NOT LIMITED TO, THE
;; IMPLIED WARRANTIES OF MERCHANTABILITY AND FITNESS FOR A PARTICULAR PURPOSE
;; ARE DISCLAIMED. IN NO EVENT SHALL THE COPYRIGHT HOLDER OR CONTRIBUTORS BE
;; LIABLE FOR ANY DIRECT, INDIRECT, INCIDENTAL, SPECIAL, EXEMPLARY, OR
;; CONSEQUENTIAL DAMAGES (INCLUDING, BUT NOT LIMITED TO, PROCUREMENT OF
;; SUBSTITUTE GOODS OR SERVICES; LOSS OF USE, DATA, OR PROFITS; OR BUSINESS
;; INTERRUPTION) HOWEVER CAUSED AND ON ANY THEORY OF LIABILITY, WHETHER IN
;; CONTRACT, STRICT LIABILITY, OR TORT (INCLUDING NEGLIGENCE OR OTHERWISE)
;; ARISING IN ANY WAY OUT OF THE USE OF THIS SOFTWARE, EVEN IF ADVISED OF THE
;; POSSIBILITY OF SUCH DAMAGE.
;;
;;*****************************************************************************

.define DDINFO_DEFINES_ONLY
.include "RPSLIB:ddinfo.def"
.undefine DDINFO_DEFINES_ONLY

import CodeGen.RepositoryAPI

namespace CodeGen.RepositoryAPI

    public partial class RpsStructure

        ;; Structure attribute data
        protected ms_info           ,s_info
        protected mStructureName    ,a30        ;; Structure name
        protected mDescription      ,a40        ;; Description
        protected mLongDescription  ,a1800      ;; Long description
        protected mUserText         ,a60        ;; User text
        protected mFields           ,@RpsFieldCollection
        protected mKeys             ,@RpsKeyCollection
        protected mFiles            ,@RpsFileCollection
        protected mTags             ,@RpsTagCollection
        protected mFormats          ,@RpsFormatCollection
        protected mRelations        ,@RpsRelationCollection
        protected mTagName          ,a30        ;;Name of repository tag definition
        protected mTagField         ,a30        ;;Name of structures tag field
        protected mTagValue         ,a17        ;;Tag value
        protected mTagEndValue      ,a17        ;;Tag end value

        ;; Constructor, load structure by name, load all fields
        public method RpsStructure
            required in StructureName, string
            endparams
            record
                pos ,int
                str ,string
                grp ,string
            endrecord
        proc

            str = RpsUtils.UpperCase(StructureName)
            grp = ""

            RpsUtils.DbgLog("Constructor    : RpsStructure("+str+")")
            Repository.CheckIsOpen()

            ;; Do we have a period in the requested structure name?
            if (pos=%instr(1,str,"."))
            begin
                if ((pos>1)&&(pos<str.Length)) then
                begin
                    grp = str(pos+1,str.Length)
                    str = str(1:pos-1)
                    RpsUtils.DbgLog("               : Structure: " + str + " Group: " + grp)
                end
                else
                    throw new RpsStructureException("Invalid group path "+str)
            end

            ;; Load the structure
            LoadStructure(str)

            ;; If we're using an implicit group then adjust the structure accordingly
            if (grp.Length>0)
            begin
                data ix, int
                data gf, @RpsField
                data cf, @RpsField
                data newLen, int

                ;; Remove all fields except for the specified group
                ix = -1
                while ((ix+=1)<this.Fields.Count)
                begin
                    if (this.Fields[ix].Name.eqs.grp) then
                        gf = mFields[ix]
                    else
                    begin
                        mFields.RemoveAt(ix)
                        ix-=1
                    end
                end

                ;;Make sure the specified group exists and is an explicit group
                if (gf==^null) then
                    throw new RpsFieldNotFoundException(str,grp)
                else
                begin
                    if (!gf.IsGroup)
                        throw new RpsFieldException("Field "+str+"."+grp+" is not a group.")
                    if (gf.GroupStructure.Length>0)
                        throw new RpsFieldException("Field "+str+"."+grp+" is an implicit group. Generate from structure "+gf.GroupStructure+" instead.")
                end

                ;; Use information from the group field to re-configure the structure
                this.Name = gf.Name
                this.Alias = gf.Name
                this.Description = gf.Description
                this.LongDescription = gf.LongDescription
                this.ChildCount = gf.GroupFields.Count
                this.FileType = "ASCII"
                this.UserText = gf.UserText
                this.FirstFile = ""

                this.mKeys = new RpsKeyCollection()
                this.mFiles = new RpsFileCollection()
                this.mTags = new RpsTagCollection()
                this.mFormats = new RpsFormatCollection()
                this.mRelations = new RpsRelationCollection()

                this.TagType = RpsTagType.None
                this.TagName = ""
                this.TagField = ""
                this.TagValue = ""
                this.TagEndValue = ""
                this.DisplayField = ""
                this.MappedStructure = ""
                this.MappedFileSpec = ""

                ;; Move the group's fields up into the structure
                foreach cf in gf.GroupFields.GetEnumerator()
                    this.Fields.Add(cf)

                ;; And remove the group field
                this.Fields.RemoveAt(0)

                ;; Set the new structure length
                ix=-1
                newLen=0
                while ((ix+=1)<mFields.Count)
                    newLen+=this.Fields[ix].Size

                this.Length = newLen

                this.IsFake = true

            end

            RpsUtils.DbgLog("Constructor    : RpsStructure("+str+") done")

        endmethod

        ;; Constructor, load structure by name, fields passed in
        public method RpsStructure
            required in StructureName, string
            required in Fields, @RpsFieldCollection
            endparams
            record
                str ,string
            endrecord
        proc
            str = %atrim(StructureName)
            RpsUtils.DbgLog("Constructor    : RpsStructure(StructureName="+str+",ExistingFields="+%string(Fields.Count)+")")
            mFields = Fields
            Repository.CheckIsOpen()
            LoadStructure(StructureName)
            RpsUtils.DbgLog("Constructor    : RpsStructure(StructureName="+str+",ExistingFields="+%string(Fields.Count)+") done")
        endmethod

        ;; Destructor
        method ~RpsStructure
        proc
            clear mFields, mKeys, mFiles, mTags, mFormats, mRelations
        endmethod

        ;; LoadStructure method
        private method LoadStructure, void
            required in StructureName, string
            endparams
        proc
            ;; Save the structure name
            mStructureName = RpsUtils.UpperCase(StructureName)

            RpsUtils.DbgLog("               : RpsStructure.LoadStructure(StructureName="+%atrim(mStructureName)+")")

            ;; Load structure information
            dd_struct(Repository.RpsControl,DDS_INFO,mStructureName,ms_info)
            if (Repository.RpsControl.error)
                throw new RpsStructureNotFoundException(atrim(mStructureName))
            RpsUtils.DbgLog("               : Set current structure to "+mStructureName)

            ;; Load the structures text-based attributes

            ;; Description
            if (ms_info.si_desc)
            begin
                xcall dd_struct(Repository.RpsControl,DDS_TEXT,ms_info.si_desc,mDescription)
                if (Repository.RpsControl.error)
                    throw new RpsStructureException("Failed to get description for repository structure "+mStructureName)
            end

            ;; Long description
            if (ms_info.si_ldesc)
            begin
                dd_struct(Repository.RpsControl,DDS_TEXT,ms_info.si_ldesc,mLongDescription)
                if (Repository.RpsControl.error)
                    throw new RpsStructureException("Failed to get long description for repository structure "+mStructureName)
            end

            ;; User text string
            if (ms_info.si_utext)
            begin
                dd_struct(Repository.RpsControl,DDS_TEXT,ms_info.si_utext,mUserText)
                if (Repository.RpsControl.error)
                    throw new RpsStructureException("Failed to get user text for repository structure "+mStructureName)
            end

            ;; If we don't already have a fields collection (because it was
            ;; passed into the constructor, then load the fields from RPS
            if (mFields==^null)
                mFields = new RpsFieldCollection(RpsFieldCollectionMode.Structure,mStructureName)

            ;; Now load the other collection properties

            mKeys = new RpsKeyCollection(mStructureName)
            mFiles = new RpsFileCollection(mStructureName)
            mTags = new RpsTagCollection(mStructureName)
            mFormats = new RpsFormatCollection(mStructureName)
            mRelations = new RpsRelationCollection(mStructureName)

            ;; CodeGen specific attributes
            loadCodeGenData(StructureName)

        endmethod

        ;;---------------------------------------------------------------------
        ;; Public properties to expose the structures attributes

        ;; Fields collection
        public property Fields, @RpsFieldCollection
            method get
            proc
                mreturn mFields
            endmethod
        endproperty

        ;; Keys collection
        public property Keys, @RpsKeyCollection
            method get
            proc
                mreturn mKeys
            endmethod
        endproperty

        ;; Files collection
        public property Files, @RpsFileCollection
            method get
            proc
                mreturn mFiles
            endmethod
        endproperty

        ;; Tags collection
        public property Tags, @RpsTagCollection
            method get
            proc
                mreturn mTags
            endmethod
        endproperty

        ;; Formats collection
        public property Formats, @RpsFormatCollection
            method get
            proc
                mreturn mFormats
            endmethod
        endproperty

        ;; Relations collection
        public property Relations, @RpsRelationCollection
            method get
            proc
                mreturn mRelations
            endmethod
        endproperty

        ;; Structure name (a30)
        public property Name, a
            method get
            proc
                mreturn %atrim(mStructureName)
            endmethod
            private method set
            proc
                mStructureName = value
            endmethod
        endproperty

        ;; File type (a15, ie "DBL ISAM", "ASCII")
        public property FileType, a
            method get
            proc
                mreturn %atrim(ms_info.si_filtyp)
            endmethod
            private method set
            proc
                ms_info.si_filtyp = value
            endmethod
        endproperty

        ;; Description (a40)
        public property Description, a
            method get
            proc
                mreturn %atrim(mDescription)
            endmethod
            private method set
            proc
                mDescription = value
            endmethod
        endproperty

        ;; Long description (a1800)
        public property LongDescription, a
            method get
            proc
                mreturn %atrim(mLongDescription)
            endmethod
            private method set
            proc
                mLongDescription = value
            endmethod
        endproperty

        ;; User text (a60)
        public property UserText, a
            method get
            proc
                mreturn %atrim(mUserText)
            endmethod
            private method set
            proc
                mUserText = value
            endmethod
        endproperty

        ;; Record size (d5)
        public property Length, int
            method get
            proc
                mreturn ms_info.si_recsz
            endmethod
            private method set
            proc
                ms_info.si_recsz = value
            endmethod
        endproperty

        ;; Number of fields/groups in first level (d3)
        public property ChildCount, int
            method get
            proc
                mreturn ms_info.si_childct
            endmethod
            private method set
            proc
                ms_info.si_childct = value
            endmethod
        endproperty

        ;; Structure tag type (enum RpsTagType)
        ;;   None, 0
        ;;   FieldAndValue, 1
        ;;   RecordSize, 2
        public property TagType, RpsTagType
            method get
            proc
                mreturn (RpsTagType)integer(ms_info.si_tagtyp)
            endmethod
            private method set
            proc
                ms_info.si_tagtyp = %integer(value)
            endmethod
        endproperty

        ;; First (or only) file assigned to (a30)
        public property FirstFile, a
            method get
            proc
                mreturn %atrim(ms_info.si_file)
            endmethod
            private method set
            proc
                ms_info.si_file = value
            endmethod
        endproperty

        ;; Name of repository tag definition (a30)
        public property TagName, a30
            method get
            proc
                mreturn %atrim(mTagName)
            endmethod
            method set
            proc
                mTagName = value
            endmethod
        endproperty

        ;; Name of structures tag field (a30)
        public property TagField, a30
            method get
            proc
                mreturn %atrim(mTagField)
            endmethod
            method set
            proc
                mTagField = value
            endmethod
        endproperty

        ;; Tag value (a17)
        public property TagValue, a17
            method get
            proc
                mreturn %atrim(mTagValue)
            endmethod
            method set
            proc
                mTagValue = value
            endmethod
        endproperty

        ;; Tag end value (a17)
        public property TagEndValue, a17
            method get
            proc
                mreturn %atrim(mTagEndValue)
            endmethod
            method set
            proc
                mTagEndValue = value
            endmethod
        endproperty

    endclass

endnamespace


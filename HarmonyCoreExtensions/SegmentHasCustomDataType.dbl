
import System
import System.Collections.Generic
import CodeGen.Engine
import CodeGen.RepositoryAPI

namespace HarmonyCoreExtensions

    public class SegmentHasCustomDataType implements IExpressionToken

        public property TokenName, String
            method get
            proc
                mreturn "HARMONYCORE_CUSTOM_SEGMENT_DATATYPE"
            endmethod
        endproperty

        public property Description, String
            method get
            proc
                mreturn "Does the segment's associated field have a custom data type?"
            endmethod
        endproperty

        public property Validity, TokenValidity
            method get
            proc
                mreturn TokenValidity.KeySegmentLoop
            endmethod
        endproperty

        public method Evaluate, Boolean
            tkn, @Token
            template, @FileNode 
            loops, @IEnumerable<LoopNode>
            endparams
        proc
            lambda doEvaluate(str, ky, segment, index, field)
            begin
                data textToSearch = field.LongDescription + field.UserText
                ;Add code here to determine the result of the expression, and return true or false
                if (textToSearch.Contains("HARMONYCORE_CUSTOM_FIELD_DATATYPE="))
                begin
                    data startPos = textToSearch.IndexOf("HARMONYCORE_CUSTOM_FIELD_DATATYPE=")
                    data semiPos = -1
                    if (startPos!=-1)
                        semiPos = textToSearch.IndexOf(";",startPos)
                    mreturn (startPos!=-1) && (semiPos!=-1) && (semiPos>(startPos+34))
                end
                mreturn false
            end
            mreturn ExpressionEvaluator.EvaluateSegmentLoopExpression(tkn, template, loops, doEvaluate)
        endmethod

    endclass

endnamespace

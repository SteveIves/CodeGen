;;*****************************************************************************
;;
;; Title:       FieldProperty.dbl
;;
;; Type:        Class
;;
;; Description: A custom field loop expansion token for use with Harmony Core
;;
;; Date:        13th March 2019
;;
;; Author:      Steve Ives, Synergex Professional Services Group
;;              http://www.synergex.com
;;
;;*****************************************************************************
;;
;; Copyright (c) 2019, Synergex International, Inc.
;; All rights reserved.
;;
;; Redistribution and use in source and binary forms, with or without
;; modification, are permitted provided that the following conditions are met:
;;
;; * Redistributions of source code must retain the above copyright notice,
;;   this list of conditions and the following disclaimer.
;;
;; * Redistributions in binary form must reproduce the above copyright notice,
;;   this list of conditions and the following disclaimer in the documentation
;;   and/or other materials provided with the distribution.
;;
;; THIS SOFTWARE IS PROVIDED BY THE COPYRIGHT HOLDERS AND CONTRIBUTORS "AS IS"
;; AND ANY EXPRESS OR IMPLIED WARRANTIES, INCLUDING, BUT NOT LIMITED TO, THE
;; IMPLIED WARRANTIES OF MERCHANTABILITY AND FITNESS FOR A PARTICULAR PURPOSE
;; ARE DISCLAIMED. IN NO EVENT SHALL THE COPYRIGHT HOLDER OR CONTRIBUTORS BE
;; LIABLE FOR ANY DIRECT, INDIRECT, INCIDENTAL, SPECIAL, EXEMPLARY, OR
;; CONSEQUENTIAL DAMAGES (INCLUDING, BUT NOT LIMITED TO, PROCUREMENT OF
;; SUBSTITUTE GOODS OR SERVICES; LOSS OF USE, DATA, OR PROFITS; OR BUSINESS
;; INTERRUPTION) HOWEVER CAUSED AND ON ANY THEORY OF LIABILITY, WHETHER IN
;; CONTRACT, STRICT LIABILITY, OR TORT (INCLUDING NEGLIGENCE OR OTHERWISE)
;; ARISING IN ANY WAY OUT OF THE USE OF THIS SOFTWARE, EVEN IF ADVISED OF THE
;; POSSIBILITY OF SUCH DAMAGE.
;;
;;*****************************************************************************

import System
import System.Collections.Generic
import System.Text
import CodeGen.Engine
import CodeGen.RepositoryAPI

namespace HarmonyCoreExtensions

    public class FieldProperty implements IExpansionToken

        public property TokenName, String
            method get
            proc
                mreturn "HARMONYCORE_FIELD_PROPERTY"
            endmethod
        endproperty

        public property Description, String
            method get
            proc
                mreturn "Produces a property to represent a field in a Harmony Core data model class."
            endmethod
        endproperty

        public property Validity, TokenValidity
            method get
            proc
                mreturn TokenValidity.FieldLoop
            endmethod
        endproperty

        public property TokenCase, TokenCaseMode
            method get
            proc
                mreturn TokenCaseMode.UppercaseOnly
            endmethod
        endproperty

        public method Expand, String
            tkn,        @Token
            template,   @FileNode 
            loops,      @IEnumerable<LoopNode>
            endparams
        proc
            lambda doExpand(str, field)
            begin
                data f, @RpsField, field
                data sb = new StringBuilder()

                sb.AppendLine(String.Format("public property {0}, {1}",TokenExpander.ChangeResultCase(f.SqlName,TokenModifier.PascalCase),f.CsType))
                sb.AppendLine("    method get")
                sb.AppendLine("    proc")
                sb.AppendLine("        mreturn mSynergyData." + f.OriginalNameModified.ToLower())
                sb.AppendLine("    endmethod")
                sb.AppendLine("    method set")
                sb.AppendLine("    proc")
                sb.AppendLine("        mSynergyData." + f.OriginalNameModified.ToLower() + " = value")
                sb.AppendLine("    endmethod")

                mreturn sb.ToString()
            end
            mreturn TokenExpander.ExpandFieldLoopToken(tkn, template, loops, doExpand)
        endmethod

    endclass

endnamespace

;;*****************************************************************************
;;
;; Title:       HarmonyRoles.dbl
;;
;; Type:        Class
;;
;; Description: A custom field loop exansion token and expression for use with Harmony Core
;;
;; Date:        21st August 2020
;;
;; Author:      Steve Ives, Synergex Professional Services Group
;;              http://www.synergex.com
;;
;;*****************************************************************************
;;
;; Copyright (c) 2020, Synergex International, Inc.
;; All rights reserved.
;;
;; Redistribution and use in source and binary forms, with or without
;; modification, are permitted provided that the following conditions are met:
;;
;; * Redistributions of source code must retain the above copyright notice,
;;   this list of conditions and the following disclaimer.
;;
;; * Redistributions in binary form must reproduce the above copyright notice,
;;   this list of conditions and the following disclaimer in the documentation
;;   and/or other materials provided with the distribution.
;;
;; THIS SOFTWARE IS PROVIDED BY THE COPYRIGHT HOLDERS AND CONTRIBUTORS "AS IS"
;; AND ANY EXPRESS OR IMPLIED WARRANTIES, INCLUDING, BUT NOT LIMITED TO, THE
;; IMPLIED WARRANTIES OF MERCHANTABILITY AND FITNESS FOR A PARTICULAR PURPOSE
;; ARE DISCLAIMED. IN NO EVENT SHALL THE COPYRIGHT HOLDER OR CONTRIBUTORS BE
;; LIABLE FOR ANY DIRECT, INDIRECT, INCIDENTAL, SPECIAL, EXEMPLARY, OR
;; CONSEQUENTIAL DAMAGES (INCLUDING, BUT NOT LIMITED TO, PROCUREMENT OF
;; SUBSTITUTE GOODS OR SERVICES; LOSS OF USE, DATA, OR PROFITS; OR BUSINESS
;; INTERRUPTION) HOWEVER CAUSED AND ON ANY THEORY OF LIABILITY, WHETHER IN
;; CONTRACT, STRICT LIABILITY, OR TORT (INCLUDING NEGLIGENCE OR OTHERWISE)
;; ARISING IN ANY WAY OUT OF THE USE OF THIS SOFTWARE, EVEN IF ADVISED OF THE
;; POSSIBILITY OF SUCH DAMAGE.
;;
;;*****************************************************************************

import System
import System.Collections.Generic
import CodeGen.Engine
import CodeGen.RepositoryAPI

namespace HarmonyCoreExtensions

    public class HarmonyRolesExpander implements IExpansionToken

        public property TokenName, String
            method get
            proc
                mreturn "HARMONY_ROLES"
            endmethod
        endproperty

        public property Description, String
            method get
            proc
                mreturn "Custom roles assigned to a field"
            endmethod
        endproperty

        public property Validity, TokenValidity
            method get
            proc
                mreturn TokenValidity.FieldLoop | TokenValidity.KeySegmentLoop | TokenValidity.RelationSegmentLoop
            endmethod
        endproperty

        public property TokenCase, TokenCaseMode
            method get
            proc
                mreturn TokenCaseMode.UppercaseOnly
            endmethod
        endproperty

        public method Expand, String
            tkn, @Token
            template, @FileNode 
            loops, @IEnumerable<LoopNode>
            endparams
        proc
            lambda doExpand(str, field)
            begin
                data propValue, string, field.GetProperty<string>(template, "HARMONY_ROLES")
                if(propValue != ^null) then
                    mreturn propValue
                else
                begin
                    mreturn ""
                end
            end
            mreturn TokenExpander.ExpandFieldLoopToken(tkn, template, loops, doExpand)
        endmethod

    endclass

    public class HarmonyRolesEvaluator implements IExpressionToken

        public property TokenName, String
            method get
            proc
                mreturn "HARMONY_ROLES"
            endmethod
        endproperty

        public property Description, String
            method get
            proc
                mreturn "Are there any custom roles defined for the field?"
            endmethod
        endproperty

        public property Validity, TokenValidity
            method get
            proc
                mreturn TokenValidity.FieldLoop | TokenValidity.KeySegmentLoop | TokenValidity.RelationSegmentLoop
            endmethod
        endproperty

        public method Evaluate, Boolean
            tkn, @Token
            template, @FileNode 
            loops, @IEnumerable<LoopNode>
            endparams
        proc
            lambda doEvaluate(str, field, index)
            begin
                data startPos = field.LongDescription.IndexOf("HARMONY_ROLES=")
                data semiPos = -1
                if (startPos!=-1)
                    semiPos = field.LongDescription.IndexOf(";",startPos)
                mreturn (startPos!=-1) && (semiPos!=-1) && (semiPos>(startPos+14))
            end
            mreturn ExpressionEvaluator.EvaluateFieldLoopExpression(tkn, template, loops, doEvaluate)
        endmethod

    endclass

endnamespace

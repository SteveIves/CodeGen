;;*****************************************************************************
;;
;; Title:       ReturnValueBridgeType.dbl
;;
;; Type:        Class
;;
;; Description: A custom method loop expansion token for use with Harmony Core
;;
;; Date:        28th June 2019
;;
;; Author:      Steve Ives, Synergex Professional Services Group
;;              http://www.synergex.com
;;
;;*****************************************************************************
;;
;; Copyright (c) 2019, Synergex International, Inc.
;; All rights reserved.
;;
;; Redistribution and use in source and binary forms, with or without
;; modification, are permitted provided that the following conditions are met:
;;
;; * Redistributions of source code must retain the above copyright notice,
;;   this list of conditions and the following disclaimer.
;;
;; * Redistributions in binary form must reproduce the above copyright notice,
;;   this list of conditions and the following disclaimer in the documentation
;;   and/or other materials provided with the distribution.
;;
;; THIS SOFTWARE IS PROVIDED BY THE COPYRIGHT HOLDERS AND CONTRIBUTORS "AS IS"
;; AND ANY EXPRESS OR IMPLIED WARRANTIES, INCLUDING, BUT NOT LIMITED TO, THE
;; IMPLIED WARRANTIES OF MERCHANTABILITY AND FITNESS FOR A PARTICULAR PURPOSE
;; ARE DISCLAIMED. IN NO EVENT SHALL THE COPYRIGHT HOLDER OR CONTRIBUTORS BE
;; LIABLE FOR ANY DIRECT, INDIRECT, INCIDENTAL, SPECIAL, EXEMPLARY, OR
;; CONSEQUENTIAL DAMAGES (INCLUDING, BUT NOT LIMITED TO, PROCUREMENT OF
;; SUBSTITUTE GOODS OR SERVICES; LOSS OF USE, DATA, OR PROFITS; OR BUSINESS
;; INTERRUPTION) HOWEVER CAUSED AND ON ANY THEORY OF LIABILITY, WHETHER IN
;; CONTRACT, STRICT LIABILITY, OR TORT (INCLUDING NEGLIGENCE OR OTHERWISE)
;; ARISING IN ANY WAY OUT OF THE USE OF THIS SOFTWARE, EVEN IF ADVISED OF THE
;; POSSIBILITY OF SUCH DAMAGE.
;;
;;*****************************************************************************

import System
import System.Collections.Generic
import CodeGen.Engine
import CodeGen.MethodCatalogAPI
import CodeGen.RepositoryAPI
import System.IO
import Newtonsoft.Json
import System.Linq
import Newtonsoft.Json.Linq

namespace HarmonyCoreExtensions

    public class ReturnValueBridgeType implements IExpansionToken

        public property TokenName, String
            method get
            proc
                mreturn "HARMONYCORE_BRIDGE_RETURN_TYPE"
            endmethod
        endproperty

        public property Description, String
            method get
            proc
                mreturn "Generates a .NET type for a method return value for use with Harmony Core Traditional Bridge."
            endmethod
        endproperty

        public property Validity, TokenValidity
            method get
            proc
                mreturn TokenValidity.MethodLoop
            endmethod
        endproperty

        public property TokenCase, TokenCaseMode
            method get
            proc
                mreturn TokenCaseMode.UppercaseOnly
            endmethod
        endproperty

        public method Expand, String
            tkn, @Token
            template, @FileNode 
            loops, @IEnumerable<LoopNode>
        proc
            lambda doExpand(catalog,iface,meth)
            begin
                using meth.ReturnValue.Type select
                (ReturnType.None),
                begin
                    mreturn "void"
                end
                (ReturnType.Alpha),
                begin
                    mreturn "String"
                end
                (ReturnType.Decimal),
                begin
                    using meth.ReturnValue.CoerceType select
                    (CoerceType.None),
                        mreturn meth.ReturnValue.Size <= 9 ? "int" : "long"
                    (CoerceType.Byte),
                        mreturn "byte"
                    (CoerceType.Short),
                        mreturn "short"
                    (CoerceType.Int),
                        mreturn "int"
                    (CoerceType.Long),
                        mreturn "long"
                    (CoerceType.Sbyte),
                        mreturn "sbyte"
                    (CoerceType.Ushort),
                        mreturn "ushort"
                    (CoerceType.Uint),
                        mreturn "uint"
                    (CoerceType.Ulong),
                        mreturn "ulong"
                    (CoerceType.Boolean),
                        mreturn "boolean"
                    (CoerceType.DateTime),
                        mreturn "@DateTime"
                    (CoerceType.DateTimeNullable),
                        mreturn "@Nullable<Datetime>"
                    (CoerceType.Decimal),
                        mreturn "decimal"
                    (CoerceType.DecimalNullable),
                        mreturn "@Nullable<decimal>"
                    endusing
                end
                (ReturnType.ImpliedDecimal),
                begin
                    using meth.ReturnValue.CoerceType select
                    (CoerceType.None),
                        mreturn "decimal"
                    (CoerceType.Decimal),
                        mreturn "decimal"
                    (CoerceType.Double),
                        mreturn "double"
                    (CoerceType.Float),
                        mreturn "float"
                    endusing
                end
                (ReturnType.Integer),
                begin
                    using meth.ReturnValue.CoerceType select
                    (CoerceType.None),
                    begin
                        using meth.ReturnValue.Size select
                        (1),
                            mreturn "byte"
                        (2),
                            mreturn "short"
                        (4),
                            mreturn "int"
                        (8),
                            mreturn "long"
                        endusing
                    end
                    (CoerceType.Byte),
                        mreturn "byte"
                    (CoerceType.Short),
                        mreturn "short"
                    (CoerceType.Int),
                        mreturn "int"
                    (CoerceType.Long),
                        mreturn "long"
                    (CoerceType.Sbyte),
                        mreturn "sbyte"
                    (CoerceType.Ushort),
                        mreturn "ushort"
                    (CoerceType.Uint),
                        mreturn "uint"
                    (CoerceType.Ulong),
                        mreturn "ulong"
                    (CoerceType.Boolean),
                        mreturn "boolean"
                    endusing
                end
                (ReturnType.HatVal),
                begin
                    mreturn "int"
                end
                (ReturnType.String),
                begin
                    mreturn "string"
                end
                (ReturnType.Enumeration),
                begin
                    mreturn meth.ReturnValue.EnumName
                end
                endusing
                mreturn ""
            end
            mreturn TokenExpander.ExpandMethodLoopToken(tkn, template, loops, doExpand)
        endmethod

    endclass

endnamespace

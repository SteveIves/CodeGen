;;*****************************************************************************
;;
;; Title:       ParameterBridgeTypeCS.dbl
;;
;; Type:        Class
;;
;; Description: A custom parameter loop expansion token for use with Harmony Core
;;
;; Date:        7th September 2022
;;
;; Author:      Steve Ives, Synergex Professional Services Group
;;              http://www.synergex.com
;;
;;*****************************************************************************
;;
;; Copyright (c) 2022, Synergex International, Inc.
;; All rights reserved.
;;
;; Redistribution and use in source and binary forms, with or without
;; modification, are permitted provided that the following conditions are met:
;;
;; * Redistributions of source code must retain the above copyright notice,
;;   this list of conditions and the following disclaimer.
;;
;; * Redistributions in binary form must reproduce the above copyright notice,
;;   this list of conditions and the following disclaimer in the documentation
;;   and/or other materials provided with the distribution.
;;
;; THIS SOFTWARE IS PROVIDED BY THE COPYRIGHT HOLDERS AND CONTRIBUTORS "AS IS"
;; AND ANY EXPRESS OR IMPLIED WARRANTIES, INCLUDING, BUT NOT LIMITED TO, THE
;; IMPLIED WARRANTIES OF MERCHANTABILITY AND FITNESS FOR A PARTICULAR PURPOSE
;; ARE DISCLAIMED. IN NO EVENT SHALL THE COPYRIGHT HOLDER OR CONTRIBUTORS BE
;; LIABLE FOR ANY DIRECT, INDIRECT, INCIDENTAL, SPECIAL, EXEMPLARY, OR
;; CONSEQUENTIAL DAMAGES (INCLUDING, BUT NOT LIMITED TO, PROCUREMENT OF
;; SUBSTITUTE GOODS OR SERVICES; LOSS OF USE, DATA, OR PROFITS; OR BUSINESS
;; INTERRUPTION) HOWEVER CAUSED AND ON ANY THEORY OF LIABILITY, WHETHER IN
;; CONTRACT, STRICT LIABILITY, OR TORT (INCLUDING NEGLIGENCE OR OTHERWISE)
;; ARISING IN ANY WAY OUT OF THE USE OF THIS SOFTWARE, EVEN IF ADVISED OF THE
;; POSSIBILITY OF SUCH DAMAGE.
;;
;;*****************************************************************************

import System
import System.Collections.Generic
import CodeGen.Engine
import CodeGen.MethodCatalogAPI
import CodeGen.RepositoryAPI
import System.IO
import Newtonsoft.Json
import System.Linq
import Newtonsoft.Json.Linq

namespace HarmonyCoreExtensions

    public class ParameterBridgeTypeCS implements IExpansionToken

        public property TokenName, String
            method get
            proc
                mreturn "HARMONYCORE_BRIDGE_PARAMETER_TYPE_CS"
            endmethod
        endproperty

        public property Description, String
            method get
            proc
                mreturn "Generates a .NET type for a method parameter for use with Harmony Core Traditional Bridge."
            endmethod
        endproperty

        public property Validity, TokenValidity
            method get
            proc
                mreturn TokenValidity.ParameterLoop
            endmethod
        endproperty

        public property TokenCase, TokenCaseMode
            method get
            proc
                mreturn TokenCaseMode.UppercaseOnly
            endmethod
        endproperty

        public method Expand, String
            tkn, @Token
            template, @FileNode 
            loops, @IEnumerable<LoopNode>
        proc
            lambda doExpand(catalog,iface,meth,param)
            begin
                data result, string, String.Empty
                data isValueType, boolean, true

                using param.Type select
                (ParameterType.Alpha),
                begin
                    result = "string"
                    isValueType = false
                end
                (ParameterType.Decimal),
                begin
                    using param.CoerceType select
                    (CoerceType.None),
                        result = param.Size <= 9 ? "int" : "long"
                    (CoerceType.Byte),
                        result = "byte"
                    (CoerceType.Short),
                        result = "short"
                    (CoerceType.Int),
                        result = "int"
                    (CoerceType.Long),
                        result = "long"
                    (CoerceType.Sbyte),
                        result = "sbyte"
                    (CoerceType.Ushort),
                        result = "ushort"
                    (CoerceType.Uint),
                        result = "uint"
                    (CoerceType.Ulong),
                        result = "ulong"
                    (CoerceType.Boolean),
                        result = "bool"
                    (CoerceType.DateTime),
                        result = "DateTime"
                    (CoerceType.DateTimeNullable),
                    begin
                        result = "DateTime?"
                        isValueType = false
                    end
                    (CoerceType.Decimal),
                        result = "decimal"
                    (CoerceType.DecimalNullable),
                    begin
                        result = "decimal?"
                        isValueType = false
                    end
                    endusing
                end
                (ParameterType.ImpliedDecimal),
                begin
                    using param.CoerceType select
                    (CoerceType.None),
                        result = "decimal"
                    (CoerceType.Decimal),
                        result = "decimal"
                    (CoerceType.Double),
                        result = "double"
                    (CoerceType.Float),
                        result = "float"
                    endusing
                end
                (ParameterType.Integer),
                begin
                    using param.CoerceType select
                    (CoerceType.None),
                    begin
                        using param.Size select
                        (1),
                            result = "byte"
                        (2),
                            result = "short"
                        (4),
                            result = "int"
                        (8),
                            result = "long"
                        endusing
                    end
                    (CoerceType.Byte),
                        result = "byte"
                    (CoerceType.Short),
                        result = "short"
                    (CoerceType.Int),
                        result = "int"
                    (CoerceType.Long),
                        result = "long"
                    (CoerceType.Sbyte),
                        result = "sbyte"
                    (CoerceType.Ushort),
                        result = "ushort"
                    (CoerceType.Uint),
                        result = "uint"
                    (CoerceType.Ulong),
                        result = "ulong"
                    (CoerceType.Boolean),
                        result = "bool"
                    endusing
                end
                (ParameterType.Structure),
                begin
                    ;;We need to use the "NoAlias" form of the structure name
                    ;;and also take into account any structure alias that is
                    ;;in place in the CodeGen environment, because the structure
                    ;;name here is the original structure name from the SMC.

                    ;;Locate the repository structure and use the alias
                    data str, @RpsStructure, template.Context.Repository.Structures.FirstOrDefault(lambda(s) {s.Name.Equals(param.StructureName)} )

                    if (str!=^null) then
                    begin
                        data typeName, string
                        if (template.Context.CurrentTask.NoCustomPluralization) then
                        begin
                            typeName = str.Alias
                        end
                        else
                        begin
                            typeName = str.Alias.EndsWith("S") ? str.Alias.Substring(0,str.Alias.Length-1) : str.Alias
                        end
                        result = StringTools.PascalCase(typeName)
                        isValueType = false
                    end
                    else
                    begin
                        ;;No match, do the best we can
                        data typeName, string
                        if (template.Context.CurrentTask.NoCustomPluralization) then
                        begin
                            typeName = param.StructureName
                        end
                        else
                        begin
                            typeName = param.StructureName.EndsWith("S") ? param.StructureName.Substring(0,param.StructureName.Length-1) : param.StructureName
                        end
                        result = StringTools.PascalCase(typeName)
                        isValueType = false
                    end
                end
                (ParameterType.Handle),
                begin
                    result = "string"
                    isValueType = false
                end
                (ParameterType.BinaryHandle),
                    result = "byte[]"
                (ParameterType.String),
                begin
                    result = "string"
                    isValueType = false
                end
                (ParameterType.Enumeration),
                    result = param.EnumName
                endusing

                ;;If the parameter is optional and represented by a value type, make it nullable
                if (!param.Required && isValueType)
                begin
                    result = String.Format("{0}?",result)
                end

                mreturn result
            end
            mreturn TokenExpander.ExpandParameterLoopToken(tkn, template, loops, doExpand)
        endmethod

    endclass

endnamespace

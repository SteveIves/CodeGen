
import System
.ifdef DBLNET
import System.Collections.ObjectModel
import System.ComponentModel
import System.IO
import System.Text
import System.Xml
import System.Xml.Serialization
.endc

import CodeGen.Engine

namespace CodeGen.Engine

.ifndef DBLNET
	
	public class CodeGenTaskSet
		
		public method CodeGenTaskSet
			endparams
		proc
			throw new Exception("The CodeGenTaskSet class is only available in Synergy .NET!")
		endmethod
		
	endclass
	
.else
	
	public class CodeGenTaskSet implements INotifyPropertyChanged

		private mDescription,			String,			String.Empty
		private mBaseFolder,			String,			String.Empty
		private mRepositoryMainFile,	String,			String.Empty
		private mRepositoryTextFile,	String,			String.Empty
		private mDefaultTemplateFolder,	String,			String.Empty
		private mDefaultOutputFolder,	String,			String.Empty
		private mLoggingLevel,			LoggingLevel,	LoggingLevel.Normal				;; -v -d, etc.
		private mListGeneratedFiles,	Boolean,		false							;;-lf
		private mTasks,					@ObservableCollection<CodeGenTask>

		public method CodeGenTaskSet
			endparams
		proc
			Description = "New Task Set"
			LoggingLevel = LoggingLevel.Normal
			Tasks = new ObservableCollection<CodeGenTask>()
		endmethod

		public property Description, String
			method get
			proc
				mreturn mDescription
			endmethod
			method set
			proc
				mDescription = value
				NotifyPropertyChanged("Description")
			endmethod
		endproperty

		public property BaseFolder, String
			method get
			proc
				mreturn mBaseFolder
			endmethod
			method set
			proc
				mBaseFolder = value
				NotifyPropertyChanged("BaseFolder")
			endmethod
		endproperty

		public property RepositoryMainFile, String
			method get
			proc
				mreturn mRepositoryMainFile
			endmethod
			method set
			proc
				mRepositoryMainFile = value
				NotifyPropertyChanged("RepositoryMainFile")
			endmethod
		endproperty
		
		public property RepositoryTextFile, String
			method get
			proc
				mreturn mRepositoryTextFile
			endmethod
			method set
			proc
				mRepositoryTextFile = value
				NotifyPropertyChanged("RepositoryTextFile")
			endmethod
		endproperty

		public property DefaultTemplateFolder, String
			method get
			proc
				mreturn mDefaultTemplateFolder
			endmethod
			method set
			proc
				mDefaultTemplateFolder = value
				NotifyPropertyChanged("DefaultTemplateFolder")
			endmethod
		endproperty

		public property DefaultOutputFolder, String
			method get
			proc
				mreturn mDefaultOutputFolder
			endmethod
			method set
			proc
				mDefaultOutputFolder = value
				NotifyPropertyChanged("DefaultOutputFolder")
			endmethod
		endproperty
		
		public property LoggingLevel, LoggingLevel
			method get
			proc
				mreturn mLoggingLevel
			endmethod
			method set
			proc
				mLoggingLevel = value
				NotifyPropertyChanged("LoggingLevel")
			endmethod
		endproperty
		
		public property ListGeneratedFiles, Boolean
			method get
			proc
				mreturn mListGeneratedFiles
			endmethod
			method set
			proc
				mListGeneratedFiles = value
				NotifyPropertyChanged("ListGeneratedFiles")
			endmethod
		endproperty
		

		public property Tasks, @ObservableCollection<CodeGenTask>
			method get
			proc
				mreturn mTasks
			endmethod
			method set
			proc
				mTasks = value
			endmethod
		endproperty

.region "Serialization"

		public method SaveToFile, boolean
			required in fileSpec, String
			endparams
		proc
			data ok, boolean, true
			try
			begin
				disposable data stm, @StreamWriter, File.CreateText(fileSpec)
				if(stm!=^null)
				begin
					data xs, @XmlSerializer, new XmlSerializer(this.GetType())
					xs.Serialize(stm,this)
					stm.Close()
				end
			end
			catch (e, @Exception)
			begin
				ok = false
			end
			endtry
			mreturn ok
		endmethod
		
		public static method LoadFromFile, boolean
			required in fileSpec, String
			required out taskSet, @CodeGenTaskSet
			endparams
		proc
			data ok, boolean, true
			disposable data stm, @StreamReader
			try
			begin
				stm = File.OpenText(fileSpec)
				if (stm!=^null)
				begin
					data xs, @XmlSerializer, new XmlSerializer(^typeof(CodeGenTaskSet))
					data xr, @XmlReader, new XmlTextReader(stm)
					taskSet = (CodeGenTaskSet)xs.Deserialize(xr)
					stm.Close()
				end
			end
			catch (ex, @Exception)
			begin
				ok = false
			end
			endtry
			mreturn ok
		endmethod
.endregion


.region "INotifyPropertyChanged"
		
		public event PropertyChanged, @PropertyChangedEventHandler
		
		private method NotifyPropertyChanged, void
			required in propertyName, String
			endparams
		proc
			if (PropertyChanged!=^null)
				PropertyChanged(this, new PropertyChangedEventArgs(propertyName))
		endmethod
		
.endregion
		
	endclass

.endc

endnamespace


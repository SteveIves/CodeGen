
import System
import System.Diagnostics
import System.Windows.Input

.array 0

namespace HarmonyCoreGenerator.ViewModel
    
    ;;;  <summary>
    ;;;  A command whose sole purpose is to 
    ;;;  relay its functionality to other
    ;;;  objects by invoking delegates. The
    ;;;  default return value for the CanExecute
    ;;;  method is 'true'.
    ;;;  </summary>
    public class RelayCommand implements ICommand
        
.region "Fields"
        
        readonly _execute, @Action<object>
        readonly _canExecute, @Predicate<object>
        
.endregion
        
.region "Constructors"
        
        ;;;  <summary>
        ;;;  Creates a new command that can always execute.
        ;;;  </summary>
        ;;;  <param name="execute">The execution logic.</param>
        public method RelayCommand
            execute, @Action<object> 
            endparams
            this(execute, ^null)
        proc
        
        endmethod
        
        ;;;  <summary>
        ;;;  Creates a new command.
        ;;;  </summary>
        ;;;  <param name="execute">The execution logic.</param>
        ;;;  <param name="canExecute">The execution status logic.</param>
        public method RelayCommand
            execute, @Action<object> 
            canExecute, @Predicate<object> 
            endparams
        proc
            if (execute == ^null)
                throw new ArgumentNullException("execute")
            _execute = execute
            _canExecute = canExecute
        endmethod

.endregion

.region "ICommand Members"
        ;; [DebuggerStepThrough]
        
        public method CanExecute, boolean
            parameter, @object 
            endparams
        proc
            mreturn _canExecute == ^null ? true : _canExecute(parameter)
        endmethod
        public event CanExecuteChanged, @EventHandler, custom
            method add
            proc
                CommandManager.RequerySuggested += value
            endmethod
            method remove
            proc
                CommandManager.RequerySuggested -= value
            endmethod
        endevent
        
        public method Execute, void
            parameter, @object 
            endparams
        proc
            _execute(parameter)
        endmethod
        
.endregion

    endclass

endnamespace


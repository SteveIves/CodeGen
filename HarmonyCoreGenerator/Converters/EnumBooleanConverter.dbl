
import System
import System.Windows
import System.Windows.Data

.array 0

namespace HarmonyCoreGenerator.Converters
    
    public class EnumBooleanConverter implements IValueConverter
        
        public method Convert, @object
            value, @object 
            targetType, @Type 
            parameter, @object 
            culture, @System.Globalization.CultureInfo 
            endparams
        proc
            data parameterString, string, ^as(parameter, string)
            if (parameterString == ^null)
                mreturn DependencyProperty.UnsetValue
            if (Enum.IsDefined(value.GetType(), value) == false)
                mreturn DependencyProperty.UnsetValue
            data parameterValue, @object, Enum.Parse(value.GetType(), parameterString)
            mreturn parameterValue.Equals(value)
        endmethod
        
        public method ConvertBack, @object
            value, @object 
            targetType, @Type 
            parameter, @object 
            culture, @System.Globalization.CultureInfo 
            endparams
        proc
            data parameterString, string, ^as(parameter, string)
            if (parameterString == ^null)
                mreturn DependencyProperty.UnsetValue
            mreturn Enum.Parse(targetType, parameterString)
        endmethod
        
    endclass
endnamespace


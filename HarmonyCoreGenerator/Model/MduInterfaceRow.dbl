
import System
import System.Collections.Generic
import System.Text
import CodeGen.Engine
import CodeGen.MethodCatalogAPI
import MvvmTools
import Newtonsoft.Json

namespace HarmonyCoreGenerator.Model

    public class MduInterfaceRow  extends ModelBase

        ;;; <summary>
        ;;; Constructor for deserialization
        ;;; </summary>
        public method MduInterfaceRow
        proc
            _Interface = new SmcInterface() { Name="" }
        endmethod

        ;;; <summary>
        ;;; Constructor for initial mdu population
        ;;; </summary>
        ;;; <param name="aInterface">Interface name</param>
        public method MduInterfaceRow
            aInterface, @SmcInterface
            this()
        proc
            _Interface = aInterface
            _MethodCount = aInterface.Methods.Count
        endmethod

        ;;; <summary>
        ;;; Name of the interface that the method belongs to
        ;;; </summary>
        public property InterfaceName, String
            method get
            proc
                mreturn _Interface.Name
            endmethod
            method set
            proc
                if (!_Interface.Name.Equals(value))
                begin
                    _Interface.Name = value
                    NotifyPropertyChanged(^nameof(InterfaceName))
                end
            endmethod
        endproperty

        ;;; <summary>
        ;;; Number of methods in the interface
        ;;; </summary>
        public property MethodCount, int
            method get
            proc
                mreturn _MethodCount
            endmethod
            method set
            proc
                if (_MethodCount != value)
                begin
                    _MethodCount = value
                    NotifyPropertyChanged(^nameof(MethodCount))
                end
            endmethod
        endproperty

        ;;; <summary>
        ;;; Collection of methods
        ;;; </summary>
        {JsonIgnore}
        public property Methods, @List<SmcMethod>
            method get
            proc
                mreturn _Interface.Methods
            endmethod
        endproperty

        ;;; <summary>
        ;;; Exposed via Traditional Bridge
        ;;; </summary>
        public property Exposed, boolean
            method get
            proc
                mreturn _Exposed
            endmethod
            method set
            proc
                if (_Exposed != value)
                    _Exposed = value
                NotifyPropertyChanged(^nameof(Exposed))
            endmethod
        endproperty

        ;; Property backing fields

        private _Interface, @SmcInterface
        private _MethodCount, int
        private _Exposed, boolean, false

    endclass

endnamespace

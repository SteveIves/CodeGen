;;*****************************************************************************
;;
;; Title:       UserTokenCollection.dbl
;;
;; Type:        Class
;;
;; Description: Represents a collection of user-defined tokens
;;
;; Date:        9th November 2007
;;
;; Author:      Steve Ives, Synergex Professional Services Group
;;              http://www.synergex.com
;;
;;*****************************************************************************
;;
;; Copyright (c) 2012, Synergex International, Inc.
;; All rights reserved.
;;
;; Redistribution and use in source and binary forms, with or without
;; modification, are permitted provided that the following conditions are met:
;;
;; * Redistributions of source code must retain the above copyright notice,
;;   this list of conditions and the following disclaimer.
;;
;; * Redistributions in binary form must reproduce the above copyright notice,
;;   this list of conditions and the following disclaimer in the documentation
;;   and/or other materials provided with the distribution.
;;
;; THIS SOFTWARE IS PROVIDED BY THE COPYRIGHT HOLDERS AND CONTRIBUTORS "AS IS"
;; AND ANY EXPRESS OR IMPLIED WARRANTIES, INCLUDING, BUT NOT LIMITED TO, THE
;; IMPLIED WARRANTIES OF MERCHANTABILITY AND FITNESS FOR A PARTICULAR PURPOSE
;; ARE DISCLAIMED. IN NO EVENT SHALL THE COPYRIGHT HOLDER OR CONTRIBUTORS BE
;; LIABLE FOR ANY DIRECT, INDIRECT, INCIDENTAL, SPECIAL, EXEMPLARY, OR
;; CONSEQUENTIAL DAMAGES (INCLUDING, BUT NOT LIMITED TO, PROCUREMENT OF
;; SUBSTITUTE GOODS OR SERVICES; LOSS OF USE, DATA, OR PROFITS; OR BUSINESS
;; INTERRUPTION) HOWEVER CAUSED AND ON ANY THEORY OF LIABILITY, WHETHER IN
;; CONTRACT, STRICT LIABILITY, OR TORT (INCLUDING NEGLIGENCE OR OTHERWISE)
;; ARISING IN ANY WAY OUT OF THE USE OF THIS SOFTWARE, EVEN IF ADVISED OF THE
;; POSSIBILITY OF SUCH DAMAGE.
;;
;;*****************************************************************************

import CodeGen.Engine
import CodeGen.RepositoryAPI
.ifdef DBLNET
import System.Collections
import System.Collections.ObjectModel
.else
import System.Collections
.endc

namespace CodeGen.Engine

.ifdef DBLNET

	public static sealed class UserTokenCollection

		;;; <summary>
		;;;	This method is used to do an initial load of user defined tokens in a token file
		;;; into the Env.UserTokens collection.
		;;; </summary>
		public static method LoadTokensFromFile, void
			required in UserTokenFile, String
			endparams
			record Local_Data
				tmpch       ,int
				RecCount    ,int
				tmpbuf      ,a256
				tokenFile	,String
			endrecord
		proc

			;;This method is used by CodeGenLauncher and CodeGenerator for user token files

			init Local_Data
			tokenFile = UserTokenFile.Trim()

			try
				open(tmpch=0,i:s,tokenFile)
			catch (ex)
				throw new CodegenException("Failed to open user defined token file " + tokenFile)
			endtry
			
			VerboseLog("Loading user defined tokens from " + tokenFile,true,false)
			
			Env.UserTokens = new ObservableCollection<UserToken>()

			repeat
			begin
				
				data pos1, int, 0
				data pos2, int, 0
				data pos3, int, 0
				data pos4, int, 0
				data lastRec, boolean, false
				
				begin
					reads(tmpch,tmpbuf,eout)
					exit
		eout,       if (rdlen()) then
						lastRec = true
					else
						exitloop
				end
				
				;;Ignore blank lines
				if (!tmpbuf)
				begin
					if (lastRec) then
						exitloop
					else
						nextloop
				end
				
				;;Ignore comment lines
				if (tmpbuf==";")
				begin
					if (lastRec) then
						exitloop
					else
						nextloop
				end
				
				RecCount += 1
				
				pos1=instr(1,tmpbuf,"<")
				if (pos1) pos2=instr(pos1,tmpbuf,">")
				if (pos2) pos3=instr(pos2,tmpbuf,"</")
				if (pos3) pos4=instr(pos3,tmpbuf,">")
					
				;;Make sure the record looks like a open/close tag record
				if ((!pos1)||(!pos2)||(!pos3)||(!pos4))
					throw new CodegenException("Record " + string(RecCount) + " in user defined token file is invalid!")
					
				;;Make sure the open and close tags match
				if (tmpbuf(pos1+1,pos2-1)!=tmpbuf(pos3+2,pos4-1))
					throw new CodegenException("Record " + string(RecCount) + " in user defined token file is invalid!")
					
				;;Store details of this user token
				Env.UserTokens.Add(new UserToken(tmpbuf(pos1,pos2),tmpbuf(pos2+1,pos3-1)))
				
				if (lastRec)
					exitloop
			end
			
			close tmpch
			if (Env.UserTokens.Count&&Env.Verbose)
			begin
				data tmpToken, @UserToken
				foreach tmpToken in Env.UserTokens
					Log(" - User token " + tmpToken.Name + " = " + tmpToken.Value,false,false)
			end

		endmethod

		;;; <summary>
		;;; This method adds user defined tokens passed in via a papameter to the tokens already
		;;; present in Env.UserTokens. If a named token already exists then its value is replaced.
		;;; </summary>
		public static method AddTokens, void
			required in tokens, @ObservableCollection<UserToken>
			endparams
		proc

			;;This method is used by CodeGenerator for user tokens provided by CodeGenTask

			data token, @UserToken
			foreach token in tokens
			begin
				;;If the token name doesn't include the < and > then add them now
				if (!token.Name.Trim().StartsWith("<"))
					token.Name = "<" + token.Name.Trim()
				if (!token.Name.Trim().EndsWith(">"))
					token.Name = token.Name.Trim() + ">"

				;;Make sure names are always upper case
				token.Name = token.Name.ToUpper()
					
				;;Does the token already exist?
				data existingToken, @UserToken
				data existingTokenUpdated, Boolean, false
				foreach existingToken in Env.UserTokens
				begin
					if existingToken.Name.Equals(token.Name)
					begin
						;;Yes, update it's value
						existingToken.value = token.value
						existingTokenUpdated = true
						exitloop
					end
				end
				;;No, add the new token
				if (!existingTokenUpdated)
					Env.UserTokens.Add(token)
				VerboseLog(" - User token " + token.Name + " = " + token.value,false,false)
			end

		endmethod

		;;; <summary>
		;;; This method adds user defined tokens passed in via a parameter to the tokens already
		;;; present in Env.UserTokens. If a named token already exists then its value is replaced.
		;;; </summary>
		public static method AddTokens, void
			required in TokenArray, @ArrayList
			endparams
			record
				ix      ,int
				delim   ,int
				token   ,string
				value   ,string
			endrecord
		proc
			
			;;This methos is used by CodeGenLauncher for command-line user tokens.
			
			VerboseLog("Loading user defined tokens from command line:",true,false)
			
			if (Env.UserTokens == ^null)
				Env.UserTokens = new ObservableCollection<UserToken>()
				
			for ix from 0 thru TokenArray.Count-1
			begin
				;;Make sure the token looks right (name=value)
				data source, String, (String)TokenArray[ix]
				if (!source.Contains("=")||source.StartsWith("=")||source.EndsWith("="))
					throw new CodegenException("Command line token " + source + " is invalid!")
					
				delim = %instr(1,source,"=")
				token = "<" + source(1:delim-1) + ">"
				value = source(delim+1,%trim(source))
				
				;;If the token is already in the collection then update its value
				;;Otherwise add a new user token
				begin
					data existingToken, @UserToken
					data existingTokenUpdated, Boolean, false
					foreach existingToken in Env.UserTokens
					begin
						if (existingToken.Name.eqs.token)
						begin
							existingToken.Value = value
							existingTokenUpdated = true
							exitloop
						end
					end
					if (!existingTokenUpdated)
						Env.UserTokens.Add(new UserToken(token,value))
				end
				
				VerboseLog(" - User token " + token + " = " + value,false,false)
			end
			
		endmethod
		
	endclass

.else

    public sealed class UserTokenCollection

        private p_Collection, @ArrayList
        private p_TokenFile, a80

        ;;Constructor (start empty)
        public method UserTokenCollection
            endparams
        proc
            p_Collection = new ArrayList()
        endmethod

        ;;Constructor (load tokens from token file)
        public method UserTokenCollection
            required in UserTokenFile, a
            endparams
        proc
            p_Collection = new ArrayList()
            LoadTokensFromFile(UserTokenFile)
        endmethod

		;;Constructor (load tokens from command line array)
		public method UserTokenCollection
			required in TokenArray, @ArrayList
			endparams
		proc
			p_Collection = new ArrayList()
			AddTokens(TokenArray)
		endmethod
		
		;;Destructor
        method ~UserTokenCollection
        proc
            clear p_Collection
        endmethod

        ;;---------------------------------------------------------------------
        ;;Private methods and properties

        ;;Load user defined tokens from XML file
        private method LoadTokensFromFile, void
            required in UserTokenFile, a
            endparams
            record Local_Data
                tmpch       ,int
                RecCount    ,int
                tmpbuf      ,a256
            endrecord
        proc
            init Local_Data
            p_TokenFile = UserTokenFile

            try
                open(tmpch=0,i:s,p_TokenFile)
            catch (ex)
                throw new CodegenException("Failed to open user defined token file " + p_TokenFile)
            endtry

            VerboseLog("Loading user defined tokens from " + p_TokenFile,true,false)

            repeat
            begin

                data pos1, int, 0
                data pos2, int, 0
                data pos3, int, 0
                data pos4, int, 0
                data lastRec, boolean, false

                begin
                    reads(tmpch,tmpbuf,eout)
                    exit
eout,               if (rdlen()) then
                        lastRec = true
                    else
                        exitloop
                end

                ;;Ignore blank lines
                if (!tmpbuf)
                begin
                    if (lastRec) then
                        exitloop
                    else
                        nextloop
                end

                ;;Ignore comment lines
                if (tmpbuf==";")
                begin
                    if (lastRec) then
                        exitloop
                    else
                        nextloop
                end

                RecCount += 1

                pos1=instr(1,tmpbuf,"<")
                if (pos1) pos2=instr(pos1,tmpbuf,">")
                if (pos2) pos3=instr(pos2,tmpbuf,"</")
                if (pos3) pos4=instr(pos3,tmpbuf,">")

                ;;Make sure the record looks like a open/close tag record
                if ((!pos1)||(!pos2)||(!pos3)||(!pos4))
                    throw new CodegenException("Record " + string(RecCount) + " in user defined token file is invalid!")

                ;;Make sure the open and close tags match
                if (tmpbuf(pos1+1,pos2-1)!=tmpbuf(pos3+2,pos4-1))
                    throw new CodegenException("Record " + string(RecCount) + " in user defined token file is invalid!")

                ;;Store details of this user token
                p_Collection.Add(new UserToken(tmpbuf(pos1,pos2),tmpbuf(pos2+1,pos3-1)))

                if (lastRec)
                    exitloop
            end

            close tmpch
            if (p_Collection.Count&&Env.Verbose)
            begin
                data tmpToken, @UserToken
                foreach tmpToken in p_Collection
                    Log(" - User token " + tmpToken.Name + " = " + tmpToken.Value,false,false)
            end

        endmethod

        ;;---------------------------------------------------------------------
        ;;Public methods and properties

        public property TokenFile, a
            method get
            proc
                mreturn atrim(p_TokenFile)
            endmethod
            method set
            proc
                p_TokenFile = value
                mreturn
            endmethod
        endproperty

		;;Add command line tokens to existing collection, replacing any existing ones
		public method AddTokens, void
			required in TokenArray, @ArrayList
			endparams
			record
				ix      ,int
				delim   ,int
				token   ,string
				value   ,string
			endrecord
		proc
			
			VerboseLog("Loading user defined tokens from command line:",true,false)
			
			for ix from 0 thru TokenArray.Count - 1
			begin
				;;Make sure the token looks right (name=value)
				data source, String, (String)TokenArray[ix]
				if (!source.Contains("=")||source.StartsWith("=")||source.EndsWith("="))
					throw new CodegenException("Command line token " + source + " is invalid!")
				
				delim = %instr(1,source,"=")
				token = "<" + source(1:delim-1) + ">"
				value = source(delim+1:%trim(source))
				
				;;If the token is already in the collection then update its value
				;;Otherwise add a new user token
				begin
					data tmpToken, @UserToken
					data existingToken, Boolean, false
					foreach tmpToken in p_Collection
					begin
						if (tmpToken.Name.eqs.token)
						begin
							tmpToken.Value = value
							existingToken = true
							exitloop
						end
					end
					if (!existingToken)
						p_Collection.Add(new UserToken(token,value))
				end
				
				VerboseLog(" - User token " + token + " = " + value,false,false)
			end
			
		endmethod
		
		;;; <summary>
        ;;; Expose the internal ArrayList so consumers can use FOREACH
        ;;; </summary>
        ;;; <returns>Internal ArrayList used to store user tokens.</returns>
        public method GetEnumerator, @ArrayList
            endparams
        proc
            mreturn p_Collection
        endmethod

        ;;---------------------------------------------------------------------
        ;;The following members make the collection look just like an ArrayList

        ;;Gets or sets the number of elements that the collection can contain.
        public property Capacity, int
            method get
            proc
                mreturn p_Collection.Capacity
            endmethod
            method set
            proc
                p_Collection.Capacity = value
            endmethod
        endproperty

        ;;Gets the number of elements actually contained in the collection.
        public property Count, int
            method get
            proc
                mreturn p_Collection.Count
            endmethod
        endproperty

        ;;Gets or sets the element at the specified index.
        public property Indexer, @UserToken
            required in Index, int
            method get
            proc
                mreturn (UserToken)p_Collection[Index]
            endmethod
            method set
            proc
                p_Collection[Index] = value
            endmethod
        endproperty

        ;;Adds an element to the end of the collection.
        public method Add, int
            required in UserToken, @UserToken
            endparams
        proc
            mreturn p_Collection.Add(UserToken)
        endmethod

        ;;Removes the element at the specified index of the collection.
        public method RemoveAt, void
            required in Index, int
        proc
            p_Collection.RemoveAt(Index)
        endmethod

        ;;Searches for the specified element and returns the one-based index
        ;;of the first occurrence within the entire collection.
        public method IndexOf, int
            required in UserToken, @UserToken
        proc
            mreturn p_Collection.IndexOf(UserToken)
        endmethod

        ;;Searches for the specified element and returns the one-based index
        ;;of the first occurrence within the range of elements in the collection
        ;;that extends from the specified index to the last element.
        public method IndexOf, int
            required in UserToken, @UserToken
            required in StartIndex, int
        proc
            mreturn p_Collection.IndexOf(UserToken,StartIndex)
        endmethod

        ;;Searches for the specified element and returns the one-based index
        ;;of the first occurrence within the range of elements in the collection
        ;;that starts at the specified index and contains the specified number of
        ;;elements.
        public method IndexOf, int
            required in UserToken, @UserToken
            required in StartIndex, int
            required in Count, int
        proc
            mreturn p_Collection.IndexOf(UserToken,StartIndex,Count)
        endmethod

        ;;Removes all elements from the collection.
        public method Clear, void
        proc
            p_Collection.Clear()
        endmethod

        ;;Inserts an element into the collection at the specified index.
        public method Insert, void
            required in AtIndex, int
            required in UserToken, @UserToken
            endparams
        proc
            p_Collection.Insert(AtIndex,UserToken)
        endmethod

        ;;Searches for the specified object and returns the one-based index of
        ;;the last occurrence within the entire collection.
        public method LastIndexOf, int
            required in UserToken, @UserToken
        proc
            mreturn p_Collection.LastIndexOf(UserToken)
        endmethod

        ;;Searches for the specified object and returns the one-based index of
        ;;the last occurrence within the range of elements in the collection that
        ;;extends from the first element to the specified index.
        public method LastIndexOf, int
            required in UserToken, @UserToken
            required in StartIndex, int
        proc
            mreturn p_Collection.LastIndexOf(UserToken,StartIndex)
        endmethod

        ;;Searches for the specified object and returns the one-based index of
        ;;the last occurrence within the range of elements in the collection that
        ;;contains the specified number of elements and ends at the specified index.
        public method LastIndexOf, int
            required in UserToken, @UserToken
            required in StartIndex, int
            required in Count, int
        proc
            mreturn p_Collection.LastIndexOf(UserToken,StartIndex,Count)
        endmethod

    endclass

.endc

endnamespace

;;*****************************************************************************
;;
;; Title:       HelpProvider.dbl
;;
;; Type:        Class
;;
;; Description: A helper class for implementing context sensitive help in a
;;              WPF / MVVM application.
;;
;; Date:        16th October 2019
;;
;; Author:      Steve Ives, Synergex Professional Services Group
;;              http://www.synergex.com
;;
;;*****************************************************************************
;;
;; Copyright (c) 2019, Synergex International, Inc.
;; All rights reserved.
;;
;; Redistribution and use in source and binary forms, with or without
;; modification, are permitted provided that the following conditions are met:
;;
;; * Redistributions of source code must retain the above copyright notice,
;;   this list of conditions and the following disclaimer.
;;
;; * Redistributions in binary form must reproduce the above copyright notice,
;;   this list of conditions and the following disclaimer in the documentation
;;   and/or other materials provided with the distribution.
;;
;; THIS SOFTWARE IS PROVIDED BY THE COPYRIGHT HOLDERS AND CONTRIBUTORS "AS IS"
;; AND ANY EXPRESS OR IMPLIED WARRANTIES, INCLUDING, BUT NOT LIMITED TO, THE
;; IMPLIED WARRANTIES OF MERCHANTABILITY AND FITNESS FOR A PARTICULAR PURPOSE
;; ARE DISCLAIMED. IN NO EVENT SHALL THE COPYRIGHT HOLDER OR CONTRIBUTORS BE
;; LIABLE FOR ANY DIRECT, INDIRECT, INCIDENTAL, SPECIAL, EXEMPLARY, OR
;; CONSEQUENTIAL DAMAGES (INCLUDING, BUT NOT LIMITED TO, PROCUREMENT OF
;; SUBSTITUTE GOODS OR SERVICES; LOSS OF USE, DATA, OR PROFITS; OR BUSINESS
;; INTERRUPTION) HOWEVER CAUSED AND ON ANY THEORY OF LIABILITY, WHETHER IN
;; CONTRACT, STRICT LIABILITY, OR TORT (INCLUDING NEGLIGENCE OR OTHERWISE)
;; ARISING IN ANY WAY OUT OF THE USE OF THIS SOFTWARE, EVEN IF ADVISED OF THE
;; POSSIBILITY OF SUCH DAMAGE.
;;
;;*****************************************************************************

import System.Windows
import System.Windows.Forms
import System.Windows.Input

namespace MvvmTools

    ;;;  <summary>
    ;;;  Provider class for online help.  
    ;;;  </summary>
    public class HelpProvider
    
.region "Fields"
    
        ;;;  <summary>
        ;;;  Help topic dependency property. 
        ;;;  </summary>
        ;;;  <remarks>This property can be attached to an object such as a form or a textbox, and 
        ;;;  can be retrieved when the user presses F1 and used to display context sensitive help.</remarks>
        public static readonly HelpTopicProperty, @DependencyProperty, DependencyProperty.RegisterAttached("HelpString", ^typeof(string), ^typeof(HelpProvider))
    
.endregion Fields
    
.region "Constructors"
    
        ;;;  <summary>
        ;;;  Static constructor that adds a command binding to Application.Help, binding it to 
        ;;;  the CanExecute and Executed methods of this class. 
        ;;;  </summary>
        ;;;  <remarks>With this in place, when the user presses F1 our help will be invoked.</remarks>
        static method HelpProvider

        proc
            CommandManager.RegisterClassCommandBinding(^typeof(FrameworkElement), new CommandBinding(ApplicationCommands.Help, new ExecutedRoutedEventHandler(ShowHelpExecuted), new CanExecuteRoutedEventHandler(ShowHelpCanExecute)))
        endmethod

.endregion Constructors

.region "Methods"
    
        ;;;  <summary>
        ;;;  Getter for <see cref="HelpTopicProperty"/>. Get a help topic that's attached to an object. 
        ;;;  </summary>
        ;;;  <param name="obj">The object that the help topic is attached to.</param>
        ;;;  <returns>The help topic.</returns>
        public static method GetHelpTopic, string
            obj, @DependencyObject 
        proc
            mreturn (string)obj.GetValue(HelpTopicProperty)
        endmethod
    
        ;;;  <summary>
        ;;;  Setter for <see cref="HelpTopicProperty"/>. Attach a help topic value to an object. 
        ;;;  </summary>
        ;;;  <param name="obj">The object to which to attach the help topic.</param>
        ;;;  <param name="value">The value of the help topic.</param>
        public static method SetHelpTopic, void
            obj, @DependencyObject 
            value, string 
        proc
            obj.SetValue(HelpTopicProperty, value)
        endmethod
    
        ;;;  <summary>
        ;;;  Show help table of contents. 
        ;;;  </summary>
        public static method ShowHelpTableOfContents, void
        proc
            Help.ShowHelp(^null, "HarmonyCoreGenerator.chm", HelpNavigator.TableOfContents)
        endmethod
    
        ;;;  <summary>
        ;;;  Show a help topic in the online CHM style help. 
        ;;;  </summary>
        ;;;  <param name="helpTopic">The help topic to show. This must match exactly with the name 
        ;;;  of one of the help topic's .htm files, without the .htm extention and with spaces instead of underscores
        ;;;  in the name. For instance, to display the help topic "This_is_my_topic.htm", pass the string "This is my topic".</param>
        ;;;  <remarks>You can also pass in the help topic with the underscore replacement already done. You can also 
        ;;;  add the .htm extension. 
        ;;;  Certain characters other than spaces are replaced by underscores in RoboHelp help topic names. 
        ;;;  This method does not yet account for all those replacements, so if you really need to find a help topic
        ;;;  with one or more of those characters, do the underscore replacement before passing the topic.</remarks>
        public static method ShowHelpTopic, void
            helpTopic, string 
        proc
            data local_helpTopic_p, string, helpTopic
            ;;  Strip off trailing period.
            if (local_helpTopic_p.IndexOf(".") == local_helpTopic_p.Length - 1)
                local_helpTopic_p = local_helpTopic_p.Substring(0, local_helpTopic_p.Length - 1)
            local_helpTopic_p = local_helpTopic_p.Replace(" ", "_").Replace("\", "_").Replace("/", "_").Replace(":", "_").Replace("*", "_").Replace("?", "_").Replace("""", "_").Replace(">", "_").Replace("<", "_").Replace("|", "_") + (local_helpTopic_p.IndexOf(".htm") == -1 ? ".htm" : "")
            System.Windows.Forms.Help.ShowHelp(^null, "HarmonyCoreGenerator.chm", HelpNavigator.Topic, local_helpTopic_p)
        endmethod
    
        ;;;  <summary>
        ;;;  Whether the F1 help command can execute. 
        ;;;  </summary>
        private static method ShowHelpCanExecute, void
            sender, @Object 
            e, @CanExecuteRoutedEventArgs 
        proc
            data senderElement, @FrameworkElement, ^as(sender, FrameworkElement)
            if (HelpProvider.GetHelpTopic(senderElement) != ^null)
                e.CanExecute = true
        endmethod
    
        ;;;  <summary>
        ;;;  Execute the F1 help command. 
        ;;;  </summary>
        ;;;  <remarks>Calls ShowHelpTopic to show the help topic attached to the framework element that's the 
        ;;;  source of the call.</remarks>
        private static method ShowHelpExecuted, void
            sender, @Object 
            e, @ExecutedRoutedEventArgs 
        proc
            ShowHelpTopic(HelpProvider.GetHelpTopic(^as(sender, FrameworkElement)))
        endmethod
    
.endregion Methods

    endclass

endnamespace


;;*****************************************************************************
;;
;; Title:       KeyTabPage.dbl
;;
;; Type:        Class (Tab Page)
;;
;; Description: A tab page which displays a key.
;;
;; Date:        21st August 2012
;;
;; Author:      Steve Ives, Synergex Professional Services Group
;;              http://www.synergex.com
;;
;;*****************************************************************************
;;
;; Copyright (c) 2012, Synergex International, Inc.
;; All rights reserved.
;;
;; Redistribution and use in source and binary forms, with or without
;; modification, are permitted provided that the following conditions are met:
;;
;; * Redistributions of source code must retain the above copyright notice,
;;   this list of conditions and the following disclaimer.
;;
;; * Redistributions in binary form must reproduce the above copyright notice,
;;   this list of conditions and the following disclaimer in the documentation
;;   and/or other materials provided with the distribution.
;;
;; THIS SOFTWARE IS PROVIDED BY THE COPYRIGHT HOLDERS AND CONTRIBUTORS "AS IS"
;; AND ANY EXPRESS OR IMPLIED WARRANTIES, INCLUDING, BUT NOT LIMITED TO, THE
;; IMPLIED WARRANTIES OF MERCHANTABILITY AND FITNESS FOR A PARTICULAR PURPOSE
;; ARE DISCLAIMED. IN NO EVENT SHALL THE COPYRIGHT HOLDER OR CONTRIBUTORS BE
;; LIABLE FOR ANY DIRECT, INDIRECT, INCIDENTAL, SPECIAL, EXEMPLARY, OR
;; CONSEQUENTIAL DAMAGES (INCLUDING, BUT NOT LIMITED TO, PROCUREMENT OF
;; SUBSTITUTE GOODS OR SERVICES; LOSS OF USE, DATA, OR PROFITS; OR BUSINESS
;; INTERRUPTION) HOWEVER CAUSED AND ON ANY THEORY OF LIABILITY, WHETHER IN
;; CONTRACT, STRICT LIABILITY, OR TORT (INCLUDING NEGLIGENCE OR OTHERWISE)
;; ARISING IN ANY WAY OUT OF THE USE OF THIS SOFTWARE, EVEN IF ADVISED OF THE
;; POSSIBILITY OF SUCH DAMAGE.
;;
;;*****************************************************************************

import System
import System.Collections.Generic
import System.Text
import System.Windows.Forms
import CodeGen.RepositoryAPI

namespace RpsBrowser.Controls

    public class KeyTabPage extends TabPage

        public method KeyTabPage
            required in aKey, @RpsKey
            endparams
        proc
            ;;Set the text on the tab
            this.Text = aKey.Description

            data tlPanel, @TableLayoutPanel, new TableLayoutPanel()
            tlPanel.RowCount = aKey.Segments.Count
            tlPanel.ColumnCount = 2
            tlPanel.Dock = DockStyle.Fill
            this.Controls.Add(tlPanel)

            data rowIx, int
            for rowIx from 0 thru aKey.Segments.Count-1
            begin
                data segment, @RpsKeySegment, aKey.Segments[rowIx]

                ;;Add the prompt label
                data lbl = new Label()
                lbl.Text = segment.Field
                tlPanel.Controls.Add(lbl,0,rowIx)

                ;;Add the value textbox
                data txt = new TextBox()

                if ((segment.DataType==RpsKeySegmentDataType.SignedInteger)
                & ||(segment.DataType==RpsKeySegmentDataType.UnsignedInteger)) then
                begin
                    using segment.Length select
                    (1), txt.MaxLength = 3
                    (2), txt.MaxLength = 6
                    (4), txt.MaxLength = 11
                    (8), txt.MaxLength = 20
                    endusing
                end
                else
                    txt.MaxLength = segment.Length

                txt.Width = txt.MaxLength * 10

                tlPanel.Controls.Add(txt,1,rowIx)

            end

        endmethod
        
    endclass

endnamespace

